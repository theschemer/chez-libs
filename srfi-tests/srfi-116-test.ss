
(import (rnrs) 
        (only (scheme list) every)
        (srfi :64 testing)
        (srfi :116 ilists))

(define (itest-equal a b) (test-assert (iequal? a b)))

(test-begin "srfi-116-ilists")

(test-group "ilists"

            (test-group "ilists/constructors"
                        (define abc (ilist 'a 'b 'c))
                        (itest-equal 'a (icar abc))
                        (itest-equal 'b (icadr abc))
                        (itest-equal 'c (icaddr abc))
                        (itest-equal (ipair 2 1) (xipair 1 2))
                        (let ((abc-dot-d (ipair* 'a 'b 'c 'd)))
                          (itest-equal 'd (icdddr abc-dot-d))
                          (itest-equal (iq c c c c) (make-ilist 4 'c))
                          (itest-equal (iq 0 1 2 3) (ilist-tabulate 4 values))
                          (itest-equal (iq 0 1 2 3 4) (iiota 5)))
                        ) ; end ilists/constructors

            (test-group "ilists/predicates"
                        (test-assert (ipair? (ipair 1 2)))
                        (test-assert (proper-ilist? '()))
                        (test-assert (proper-ilist? (iq 1 2 3)))
                        (test-assert (ilist? '()))
                        (test-assert (ilist? (iq 1 2 3)))
                        (test-assert (dotted-ilist? (ipair 1 2)))
                        (test-assert (dotted-ilist? 2))
                        (test-assert (null-ilist? '()))
                        (test-assert (not (null-ilist? (iq 1 2 3))))
                        (test-error (null-ilist? 'a))
                        (test-assert (not-ipair? 'a))
                        (test-assert (not (not-ipair? (ipair 'a 'b))))
                        (test-assert (ilist= = (iq 1 2 3) (iq 1 2 3)))
                        (test-assert (not (ilist= = (iq 1 2 3 4) (iq 1 2 3))))
                        (test-assert (not (ilist= = (iq 1 2 3) (iq 1 2 3 4))))
                        (test-assert (ilist= = (iq 1 2 3) (iq 1 2 3)))
                        (test-assert (not (ilist= = (iq 1 2 3) (iq 1 2 3 4) (iq 1 2 3 4))))
                        (test-assert (not (ilist= = (iq 1 2 3) (iq 1 2 3) (iq 1 2 3 4))))
                        ) ; end ilist/predicates

            (test-group "ilist/cxrs"
                        (define ab (ipair 'a 'b))
                        (define cd (ipair 'c 'd))
                        (define ef (ipair 'e 'f))
                        (define gh (ipair 'g 'h))
                        (define abcd (ipair ab cd))
                        (define efgh (ipair ef gh))
                        (define abcdefgh (ipair abcd efgh))
                        (define ij (ipair 'i 'j))
                        (define kl (ipair 'k 'l))
                        (define mn (ipair 'm 'n))
                        (define op (ipair 'o 'p))
                        (define ijkl (ipair ij kl))
                        (define mnop (ipair mn op))
                        (define ijklmnop (ipair ijkl mnop))
                        (define abcdefghijklmnop (ipair abcdefgh ijklmnop))
                        (itest-equal 'a (icaar abcd))
                        (itest-equal 'b (icdar abcd))
                        (itest-equal 'c (icadr abcd))
                        (itest-equal 'd (icddr abcd))
                        (itest-equal 'a (icaaar abcdefgh))
                        (itest-equal 'b (icdaar abcdefgh))
                        (itest-equal 'c (icadar abcdefgh))
                        (itest-equal 'd (icddar abcdefgh))
                        (itest-equal 'e (icaadr abcdefgh))
                        (itest-equal 'f (icdadr abcdefgh))
                        (itest-equal 'g (icaddr abcdefgh))
                        (itest-equal 'h (icdddr abcdefgh))
                        (itest-equal 'a (icaaaar abcdefghijklmnop))
                        (itest-equal 'b (icdaaar abcdefghijklmnop))
                        (itest-equal 'c (icadaar abcdefghijklmnop))
                        (itest-equal 'd (icddaar abcdefghijklmnop))
                        (itest-equal 'e (icaadar abcdefghijklmnop))
                        (itest-equal 'f (icdadar abcdefghijklmnop))
                        (itest-equal 'g (icaddar abcdefghijklmnop))
                        (itest-equal 'h (icdddar abcdefghijklmnop))
                        (itest-equal 'i (icaaadr abcdefghijklmnop))
                        (itest-equal 'j (icdaadr abcdefghijklmnop))
                        (itest-equal 'k (icadadr abcdefghijklmnop))
                        (itest-equal 'l (icddadr abcdefghijklmnop))
                        (itest-equal 'm (icaaddr abcdefghijklmnop))
                        (itest-equal 'n (icdaddr abcdefghijklmnop))
                        (itest-equal 'o (icadddr abcdefghijklmnop))
                        (itest-equal 'p (icddddr abcdefghijklmnop))
                        ) ; end ilists/cxrs

            (test-group "ilists/selectors"
                        (itest-equal 'c (ilist-ref (iq a b c d) 2))
                        (let ((ten (ilist 1 2 3 4 5 6 7 8 9 10)))
                          (itest-equal 1 (ifirst ten))
                          (itest-equal 2 (isecond ten))
                          (itest-equal 3 (ithird ten))
                          (itest-equal 4 (ifourth ten))
                          (itest-equal 5 (ififth ten))
                          (itest-equal 6 (isixth ten))
                          (itest-equal 7 (iseventh ten))
                          (itest-equal 8 (ieighth ten))
                          (itest-equal 9 (ininth ten))
                          (itest-equal 10 (itenth ten)))
                        (test-error (ilist-ref '() 2))
                        (itest-equal '(1 2) (call-with-values (lambda () (icar+icdr (ipair 1 2))) list))
                        (let ((abcde (iq a b c d e))
                              (dotted (ipair 1 (ipair 2 (ipair 3 'd)))))
                          (itest-equal (iq a b) (itake abcde 2))
                          (itest-equal (iq c d e) (idrop abcde 2))
                          (itest-equal (iq c d e) (ilist-tail abcde 2))
                          (itest-equal (iq 1 2) (itake dotted 2))
                          (itest-equal (ipair 3 'd) (idrop dotted 2))
                          (itest-equal (ipair 3 'd) (ilist-tail dotted 2))
                          (itest-equal 'd (idrop dotted 3))
                          (itest-equal 'd (ilist-tail dotted 3))
                          (itest-equal abcde (iappend (itake abcde 4) (idrop abcde 4)))
                          (itest-equal (iq d e) (itake-right abcde 2))
                          (itest-equal (iq a b c) (idrop-right abcde 2))
                          (itest-equal (ipair 2 (ipair 3 'd)) (itake-right dotted 2))
                          (itest-equal (iq 1) (idrop-right dotted 2))
                          (itest-equal 'd (itake-right dotted 0))
                          (itest-equal (iq 1 2 3) (idrop-right dotted 0))
                          (itest-equal abcde (call-with-values (lambda () (isplit-at abcde 3)) iappend))
                          (itest-equal 'c (ilast (iq a b c)))
                          (itest-equal (iq c) (last-ipair (iq a b c))))
                        ) ; end ilists/selectors

            (test-group "ilists/misc"
                        (itest-equal 0 (ilength '()))
                        (itest-equal 3 (ilength (iq 1 2 3)))
                        (itest-equal (iq x y) (iappend (iq x) (iq y)))
                        (itest-equal (iq a b c d) (iappend (iq a b) (iq c d)))
                        (itest-equal (iq a) (iappend '() (iq a)))
                        (itest-equal (iq x y) (iappend (iq x y)))
                        (itest-equal '() (iappend))
                        (itest-equal (iq a b c d) (iconcatenate (iq (a b) (c d))))
                        (itest-equal (iq c b a) (ireverse (iq a b c)))
                        (itest-equal (iq (e (f)) d (b c) a) (ireverse (iq a (b c) d (e (f)))))
                        (itest-equal (ipair 2 (ipair 1 'd)) (iappend-reverse (iq 1 2) 'd))
                        (itest-equal (iq (one 1 odd) (two 2 even) (three 3 odd))
                                    (izip (iq one two three) (iq 1 2 3) (iq odd even odd)))
                        (itest-equal (iq (1) (2) (3)) (izip (iq 1 2 3)))
                        (itest-equal (iq 1 2 3) (iunzip1 (iq (1) (2) (3))))
                        (itest-equal (iq (1 2 3) (one two three))
                                    (call-with-values
                                      (lambda () (iunzip2 (iq (1 one) (2 two) (3 three))))
                                      ilist))
                        (itest-equal (iq (1 2 3) (one two three) (a b c))
                                    (call-with-values
                                      (lambda () (iunzip3 (iq (1 one a) (2 two b) (3 three c))))
                                      ilist))
                        (itest-equal (iq (1 2 3) (one two three) (a b c) (4 5 6))
                                    (call-with-values
                                      (lambda () (iunzip4 (iq (1 one a 4) (2 two b 5) (3 three c 6))))
                                      ilist))
                        (itest-equal (iq (1 2 3) (one two three) (a b c) (4 5 6) (#t #f #t))
                                    (call-with-values
                                      (lambda () (iunzip5 (iq (1 one a 4 #t) (2 two b 5 #f) (3 three c 6 #t))))
                                      ilist))
                        (itest-equal 3 (icount even? (iq 3 1 4 1 5 9 2 5 6)))
                        (itest-equal 3 (icount < (iq 1 2 4 8) (iq 2 4 6 8 10 12 14 16)))
                        ) ; end ilists/misc

            (test-group "ilists/folds"
                        ;; We have to be careful to test both single-list and multiple-list
                        ;; code paths, as they are different in this implementation.

                        (define lis (iq 1 2 3))
                        (itest-equal 6 (ifold + 0 lis))
                        (itest-equal (iq 3 2 1) (ifold ipair '() lis))
                        (itest-equal 2 (ifold
                                        (lambda (x count) (if (symbol? x) (+ count 1) count))
                                        0
                                        (iq a 0 b)))
                        (itest-equal 4 (ifold
                                        (lambda (s max-len) (max max-len (string-length s)))
                                        0
                                        (iq "ab" "abcd" "abc")))
                        (itest-equal 32 (ifold
                                         (lambda (a b ans) (+ (* a b) ans))
                                         0
                                         (iq 1 2 3)
                                         (iq 4 5 6)))
                        (let ((z (lambda (x y ans) (ipair (ilist x y) ans))))
                          (itest-equal (iq (b d) (a c))
                                      (ifold z '() (iq a b) (iq c d)))
                          (itest-equal lis (ifold-right ipair '() lis))
                          (itest-equal (iq 0 2 4) (ifold-right
                                                   (lambda (x l) (if (even? x) (ipair x l) l))
                                                   '()
                                                   (iq 0 1 2 3 4)))
                          (itest-equal (iq (a c) (b d))
                                      (ifold-right z '() (iq a b) (iq c d)))
                          (itest-equal (iq (c) (b c) (a b c))
                                      (ipair-fold ipair '() (iq a b c)))
                          (itest-equal (iq ((b) (d)) ((a b) (c d)))
                                      (ipair-fold z '() (iq a b) (iq c d)))
                          (itest-equal (iq (a b c) (b c) (c))
                                      (ipair-fold-right ipair '() (iq a b c)))
                          (itest-equal (iq ((a b) (c d)) ((b) (d)))
                                      (ipair-fold-right z '() (iq a b) (iq c d))))
                        (itest-equal 5 (ireduce max 0 (iq 1 3 5 4 2 0)))
                        (itest-equal 1 (ireduce - 0 (iq 1 2)))
                        (itest-equal -1 (ireduce-right - 0 (iq 1 2)))
                        (let ((squares (iq 1 4 9 16 25 36 49 64 81 100)))
                          (itest-equal squares
                                      (iunfold (lambda (x) (> x 10))
                                               (lambda (x) (* x x))
                                               (lambda (x) (+ x 1))
                                               1))
                          (itest-equal squares
                                      (iunfold-right zero?
                                                     (lambda (x) (* x x))
                                                     (lambda (x) (- x 1))
                                                     10)))
                        (itest-equal (iq 1 2 3) (iunfold null-ilist? icar icdr (iq 1 2 3)))
                        (itest-equal (iq 3 2 1) (iunfold-right null-ilist? icar icdr (iq 1 2 3)))
                        (itest-equal (iq 1 2 3 4)
                                    (iunfold null-ilist? icar icdr (iq 1 2) (lambda (x) (iq 3 4))))
                        (itest-equal (iq b e h) (imap icadr (iq (a b) (d e) (g h))))
                        (itest-equal (iq b e h) (imap-in-order icadr (iq (a b) (d e) (g h))))
                        (itest-equal (iq 5 7 9) (imap + (iq 1 2 3) (iq 4 5 6)))
                        (itest-equal (iq 5 7 9) (imap-in-order + (iq 1 2 3) (iq 4 5 6)))
                        (let ((z (let ((count 0)) (lambda (ignored) (set! count (+ count 1)) count))))
                          (itest-equal (iq 1 2) (imap-in-order z (iq a b)))
                          (itest-equal '#(0 1 4 9 16)
                                      (let ((v (make-vector 5)))
                                        (ifor-each (lambda (i)
                                                     (vector-set! v i (* i i)))
                                                   (iq 0 1 2 3 4))
                                        v))
                          (itest-equal '#(5 7 9 11 13)
                                      (let ((v (make-vector 5)))
                                        (ifor-each (lambda (i j)
                                                     (vector-set! v i (+ i j)))
                                                   (iq 0 1 2 3 4)
                                                   (iq 5 6 7 8 9))
                                        v))
                          (itest-equal (iq 1 -1 3 -3 8 -8)
                                      (iappend-map (lambda (x) (ilist x (- x))) (iq 1 3 8)))
                          (itest-equal (iq 1 4 2 5 3 6)
                                      (iappend-map ilist (iq 1 2 3) (iq 4 5 6)))
                          (itest-equal (vector (iq 0 1 2 3 4) (iq 1 2 3 4) (iq 2 3 4) (iq 3 4) (iq 4))
                                      (let ((v (make-vector 5)))
                                        (ipair-for-each (lambda (lis) (vector-set! v (icar lis) lis)) (iq 0 1 2 3 4))
                                        v))
                          (itest-equal (vector (iq 5 6 7 8 9) (iq 6 7 8 9) (iq 7 8 9) (iq 8 9) (iq 9))
                                      (let ((v (make-vector 5)))
                                        (ipair-for-each (lambda (i j) (vector-set! v (icar i) j))
                                                        (iq 0 1 2 3 4)
                                                        (iq 5 6 7 8 9))
                                        v))
                          (itest-equal (iq 1 9 49)
                                      (ifilter-map (lambda (x) (and (number? x) (* x x))) (iq a 1 b 3 c 7)))
                          (itest-equal (iq 5 7 9)
                                      (ifilter-map
                                        (lambda (x y) (and (number? x) (number? y) (+ x y)))
                                        (iq 1 a 2 b 3 4)
                                        (iq 4 0 5 y 6 z))))
                        ) ; end ilists/folds

            (test-group "ilists/filtering"
                        (itest-equal (iq 0 8 8 -4) (ifilter even? (iq 0 7 8 8 43 -4)))
                        (itest-equal (list (iq one four five) (iq 2 3 6))
                                    (call-with-values
                                      (lambda () (ipartition symbol? (iq one 2 3 four five 6)))
                                      list))
                        (itest-equal (iq 7 43) (iremove even? (iq 0 7 8 8 43 -4)))
                        ) ; end ilists/filtering

            (test-group "ilists/searching"
                        (itest-equal 2 (ifind even? (iq 1 2 3)))
                        (itest-equal #t (iany  even? (iq 1 2 3)))
                        (itest-equal #f (ifind even? (iq 1 7 3)))
                        (itest-equal #f (iany  even? (iq 1 7 3)))
                        (test-error (ifind even? (ipair 1 (ipair 3 'x)))) ; should give error!
                        (test-error (iany  even? (ipair 1 (ipair 3 'x))))
                        (itest-equal 4 (ifind even? (iq 3 1 4 1 5 9)))
                        (itest-equal (iq -8 -5 0 0) (ifind-tail even? (iq 3 1 37 -8 -5 0 0)))
                        (itest-equal (iq 2 18) (itake-while even? (iq 2 18 3 10 22 9)))
                        (itest-equal (iq 3 10 22 9) (idrop-while even? (iq 2 18 3 10 22 9)))
                        (itest-equal (list (iq 2 18) (iq 3 10 22 9))
                                    (call-with-values
                                      (lambda () (ispan even? (iq 2 18 3 10 22 9)))
                                      list))
                        (itest-equal (list (iq 3 1) (iq 4 1 5 9))
                                    (call-with-values
                                      (lambda () (ibreak even? (iq 3 1 4 1 5 9)))
                                      list))
                        (itest-equal #t (iany integer? (iq a 3 b 2.7)))
                        (itest-equal #f (iany integer? (iq a 3.1 b 2.7)))
                        (itest-equal #t (iany < (iq 3 1 4 1 5) (iq 2 7 1 8 2)))
                        (itest-equal #t (ievery integer? (iq 1 2 3 4 5)))
                        (itest-equal #f (ievery integer? (iq 1 2 3 4.5 5)))
                        (itest-equal #t (ievery < (iq 1 2 3) (iq 4 5 6)))
                        (itest-equal 2 (ilist-index even? (iq 3 1 4 1 5 9)))
                        (itest-equal 1 (ilist-index < (iq 3 1 4 1 5 9 2 5 6) (iq 2 7 1 8 2)))
                        (itest-equal #f (ilist-index = (iq 3 1 4 1 5 9 2 5 6) (iq 2 7 1 8 2)))
                        (itest-equal (iq a b c) (imemq 'a (iq a b c)))
                        (itest-equal (iq b c) (imemq 'b (iq a b c)))
                        (itest-equal #f (imemq 'a (iq b c d)))
                        (itest-equal #f (imemq (ilist 'a) (iq b (a) c)))
                        (test-assert (ilist? (imember (ilist 'a) (iq b (a) c) )))
                        (itest-equal (iq (a) c) (imember (ilist 'a) (iq b (a) c) ))
                        (itest-equal (iq 101 102) (imemv 101 (iq 100 101 102)))
                        ) ; end ilists/searching

            (test-group "ilists/deletion"
                        (itest-equal (iq 1 2 4 5) (idelete 3 (iq 1 2 3 4 5)))
                        (itest-equal (iq 3 4 5) (idelete 5 (iq 3 4 5 6 7) <))
                        (itest-equal (iq a b c z) (idelete-duplicates (iq a b a c a b c z)))
                        ) ; end ilists/deletion

            (test-group "ilists/alists"
                        (define e (iq (a 1) (b 2) (c 3))) 
                        (itest-equal (iq a 1) (iassq 'a e))
                        (itest-equal (iq b 2) (iassq 'b e))
                        (itest-equal #f (iassq 'd e))
                        (itest-equal #f (iassq (ilist 'a) (iq ((a)) ((b)) ((c)))))
                        (itest-equal (iq (a)) (iassoc (ilist 'a) (iq ((a)) ((b)) ((c))) ))
                        (let ((e2 (iq (2 3) (5 7) (11 13))))
                          (itest-equal (iq 5 7) (iassv 5 e2))
                          (itest-equal (iq 11 13) (iassoc 5 e2 <))
                          (itest-equal (ipair (iq 1 1) e2) (ialist-cons 1 (ilist 1) e2))
                          (itest-equal (iq (2 3) (11 13)) (ialist-delete 5 e2))
                          (itest-equal (iq (2 3) (5 7)) (ialist-delete 5 e2 <)))
                        ) ; end ilists/alists

            (test-group "ilists/replacers"
                        (itest-equal (ipair 1 3) (replace-icar (ipair 2 3) 1))
                        (itest-equal (ipair 1 3) (replace-icdr (ipair 1 2) 3))
                        ) ; end ilists/replacers

            (test-group "ilists/conversion"
                        (itest-equal (ipair 1 2) (pair->ipair '(1 . 2)))
                        (itest-equal '(1 . 2) (ipair->pair (ipair 1 2)))
                        (itest-equal (iq 1 2 3) (list->ilist '(1 2 3)))
                        (itest-equal '(1 2 3) (ilist->list (iq 1 2 3)))
                        (itest-equal (ipair 1 (ipair 2 3)) (list->ilist '(1 2 . 3)))
                        (itest-equal '(1 2 . 3) (ilist->list (ipair 1 (ipair 2 3))))
                        (itest-equal (ipair (ipair 1 2) (ipair 3 4)) (tree->itree '((1 . 2) . (3 . 4))))
                        (itest-equal '((1 . 2) . (3 . 4)) (itree->tree (ipair (ipair 1 2) (ipair 3 4))))
                        (itest-equal (ipair (ipair 1 2) (ipair 3 4)) (gtree->itree (cons (ipair 1 2) (ipair 3 4))))
                        (itest-equal '((1 . 2) . (3 . 4)) (gtree->tree (cons (ipair 1 2) (ipair 3 4))))
                        (itest-equal 6 (iapply + (iq 1 2 3)))
                        (itest-equal 15 (iapply + 1 2 (iq 3 4 5)))
                        ) ; end ilists/conversion

            ) ; end ilists

(test-end)

